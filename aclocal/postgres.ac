dnl
dnl Autoconf support for finding postgres
dnl

dnl
dnl Main macro for finding a usable postgres installation 
dnl
AC_DEFUN(AC_DTN_PGCONFIG, [
    ac_postgresdir='none'

    AC_ARG_WITH(postgres,
        AC_HELP_STRING([--with-postgres=DIR],
    		   [location of a postgres installation (default system)]),
        ac_postgresdir=$withval) 

    dnl
    dnl First make sure we even want it
    dnl
    if test x$ac_postgresdir = xnone ; then
    POSTGRES_ENABLED=0
    else
    POSTGRES_ENABLED=1

    dnl
    dnl Postgres include file
    dnl
    if test x$dtn_cv_path_postgres_h = x ; then
        echo "checking for postgres header file..."
        AC_DTN_FIND_PGINCLUDE
    else
        echo "checking for postgres header... (cached) $dtn_cv_path_postgres_h"
    fi

    if test ! $dtn_cv_path_postgres_h = /usr/include ; then
        CPPFLAGS="$CPPFLAGS -I$dtn_cv_path_postgres_h"
    fi

    dnl
    dnl Postgres library file
    dnl
    if test x$dtn_cv_path_postgres_lib = x ; then
        echo "checking for postgres library..."
        AC_DTN_FIND_PGLIB
    else
        echo "checking for postgres library... (cached) $dtn_cv_path_postgres_lib"
    fi
        
    if test ! x$dtn_cv_path_postgres_lib = x/usr/lib ; then
        LDFLAGS="$LDFLAGS -L$dtn_cv_path_postgres_lib"
    fi
    LIBS="$LIBS -lpq"
    
    fi # POSTGRES_ENABLED
    AC_SUBST(POSTGRES_ENABLED)
])

dnl
dnl Find postgres.h
dnl
AC_DEFUN(AC_DTN_FIND_PGINCLUDE, [
  AC_CACHE_VAL([dtn_cv_path_postgres_h], [
    dtn_cv_path_postgres_h=

    if test ! x"$ac_postgresdir" = x"system" ; then
        ac_postgresincdirs=$ac_postgresdir/include/
    else
        ac_postgresincdirs="/usr/include /usr/include/postgresql /usr/local/pgsql/include"
    fi

    for ac_postgresincdir in $ac_postgresincdirs; do
        AC_CHECK_FILE($ac_postgresincdir/libpq-fe.h,
	              [dtn_cv_path_postgres_h=$ac_postgresincdir
		       break])
    done
  ])

  if test x$dtn_cv_path_postgres_h = x ; then
        AC_MSG_ERROR([can't find usable postgres.h])
  fi
])


dnl
dnl Find libpostgres
dnl
AC_DEFUN(AC_DTN_FIND_PGLIB, [
    AC_CACHE_VAL(dtn_cv_path_postgres_lib,[
        if test ! x"$ac_postgresdir" = x"system" ; then
            ac_postgreslibdirs=$ac_postgresdir/lib/
        else
            ac_postgreslibdirs="/usr/lib /usr/lib/pgsql /usr/local/pgsql/lib"
        fi

        for dtn_cv_path_postgres_lib in $ac_postgreslibdirs; do
            AC_CHECK_LIB_FLAGS(pq, PQdb, [-L$dtn_cv_path_postgres_lib],
	                       [break], [dtn_cv_path_postgres_lib=])
        done
    ])

    if test x$dtn_cv_path_postgres_lib = x ; then
        AC_MSG_ERROR([can't find usable postgres library])
    fi
])
